plugins {
    id 'net.minecraftforge.gradle' version '[6.0.24,6.2)'
}
jarJar.enable()

minecraft {
    mappings channel: "official", version: "1.21.1"
    reobf = false
    copyIdeResources = true
    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
        }
        client {
            property 'forge.enabledGameTestNamespaces', "presencium"
        }
        server {
            property 'forge.enabledGameTestNamespaces', "presencium"
            args '--nogui'
        }
        gameTestServer {
            property 'forge.enabledGameTestNamespaces', "presencium"
        }
        data {
            workingDirectory project.file('run-data')
            args '--mod', "presencium", '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.21.1-52.0.16"
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
    minecraftLibrary "me.splitque:common"
    jarJar(group: 'me.splitque', name: 'common', version: '[2.0,3.0)') {
        jarJar.pin(it, '3.0')
    }
}

jarJar {
    archivesBaseName = 'presencium'
}

processResources {
    var replaceProperties = [
            version: rootProject.version
    ]
    inputs.properties replaceProperties

    filesMatching('META-INF/mods.toml') {
        expand replaceProperties + [project: project]
    }
}