plugins {
    id 'net.minecraftforge.gradle' version '[6.0.24,6.2)'
}
archivesBaseName = 'presencium'
jarJar.enable()

minecraft {
    mappings channel: "official", version: "1.21.1"
    reobf = false
    copyIdeResources = true
    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'
        }
        client {
            property 'forge.enabledGameTestNamespaces', "presencium"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.21.1-52.0.16"
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
    minecraftLibrary "me.splitque:common"
    jarJar(group: 'me.splitque', name: 'common', version: '[1.0,2.0)') {
        jarJar.pin(it, '1.0')
    }
}

processResources {
    var replaceProperties = [
            version: rootProject.version
    ]
    inputs.properties replaceProperties

    filesMatching('META-INF/mods.toml') {
        expand replaceProperties + [project: project]
    }
}
sourceSets {
    main {
        resources {
            srcDir project(':forge').file('src/main/resources')
        }
    }
}
sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}
