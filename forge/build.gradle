plugins {
    id 'net.minecraftforge.gradle' version '[6.0.24,6.2)'
}
archivesBaseName = 'presencium-FORGE'
version = "v" + rootProject.version + "+" + rootProject.minecraft_version
jarJar.enable()

minecraft {
    mappings channel: "official", version: "1.21.1"
    reobf = false
    copyIdeResources = true
    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'
        }
        client {
            property 'forge.enabledGameTestNamespaces', "presencium"
        }
    }
}

// https://files.minecraftforge.net/net/minecraftforge/forge/
dependencies {
    minecraft "net.minecraftforge:forge:1.21.1-52.0.16"
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
    minecraftLibrary "me.splitque:common"
    jarJar(group: 'me.splitque', name: 'common', version: '[2.0,4.0)') {
        jarJar.pin(it, rootProject.version)
    }
}

processResources {
    var replaceProperties = [
            version: rootProject.version
    ]
    inputs.properties replaceProperties

    filesMatching('META-INF/mods.toml') {
        expand replaceProperties + [project: project]
    }
}
sourceSets.main.resources {
    srcDir project(":resources").file('resources')
}
sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}
